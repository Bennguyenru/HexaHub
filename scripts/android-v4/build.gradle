apply plugin: 'java'

repositories {
    maven {
        url "https://maven.google.com/"
    }
    mavenCentral()
}

dependencies {
    compile 'com.google.firebase:firebase-messaging:19.0.1'
    compile 'com.google.firebase:firebase-core:17.0.1'
    compile 'com.google.android.gms:play-services-base:17.0.0'
    compile 'androidx.legacy:legacy-support-v4:1.0.0'
    compile 'androidx.core:core:1.0.2'
    compile 'androidx.legacy:legacy-support-core-utils:1.0.0'
    compile 'androidx.legacy:legacy-support-core-ui:1.0.0'
    compile 'androidx.media:media:1.0.1'
    compile 'androidx.fragment:fragment:1.0.0'
    compile 'androidx.annotation:annotation:1.1.0'
    compile 'androidx.arch.core:core-common:2.0.1'
    compile 'androidx.arch.core:core-runtime:2.0.1'
    compile 'androidx.lifecycle:lifecycle-common:2.0.0'
    compile 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    compile 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    compile 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'
    compile 'androidx.lifecycle:lifecycle-runtime:2.0.0'
}

def resultDirectory = "${project.buildDir}/result"
def packedDirectory = "${resultDirectory}/packed"
def aarDirectory = "${packedDirectory}/aar"
def unpackedDirectory = "${resultDirectory}/unpacked"
def unpackedClassesDirectory = "${unpackedDirectory}/classes"
def packageOutputDirectory = "${project.buildDir}/package"
def packageDirectory = "${packageOutputDirectory}/share/java"
def packageResDirectory = "${packageDirectory}/res"

task copyArtifacts() {
    outputs.dir packedDirectory

    project.configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        println "${artifact.moduleVersion.id.group}.${artifact.name}-${artifact.moduleVersion.id.version}.${artifact.extension}"
        copy {
            from artifact.file
            into packedDirectory
            rename "(.*)", "${artifact.moduleVersion.id.group}.${artifact.name}-${artifact.moduleVersion.id.version}.${artifact.extension}"
        }
    }
}

task unzipJars(dependsOn: copyArtifacts) {
    inputs.dir packedDirectory
    outputs.dir unpackedClassesDirectory

    doLast {
        println('*** Unzip jars')
        fileTree(dir: packedDirectory).include('*.jar').each { lib ->
            copy {
                println "Unzip $lib.name"
                from zipTree(lib)
                into unpackedClassesDirectory
                exclude 'META-INF/'
                exclude 'NOTICE.txt'
            }
        }
    }
}

task unzipAars(dependsOn: unzipJars) {
    inputs.dir packedDirectory
    outputs.dir unpackedClassesDirectory

    doLast {
        println('*** Unzip aars')
        fileTree(dir: packedDirectory).include('*.aar').each { lib ->
            copy {
                println "Unzip $lib.name"
                def dirName = lib.name - '.aar'
                from zipTree(lib)
                into "${aarDirectory}/${dirName}"
            }
        }
        println('*** Unzip jars from aars')
        fileTree(dir: "${aarDirectory}").include('**/*.jar').each { lib ->
            copy {
                println "Unzip $lib.name"
                from zipTree(lib)
                into unpackedClassesDirectory
                exclude 'META-INF/'
            }
        }
    }
}

task zipClasses(dependsOn: unzipAars, type: Zip) {
    from unpackedClassesDirectory
    archiveName 'android-support-v4.jar'
    destinationDir file("${packageDirectory}")
}

task collectResources(dependsOn: zipClasses, type:Exec) {
    commandLine 'python', 'extract_res.py', "${aarDirectory}", "${packageResDirectory}"
}

task fixResPermissions(dependsOn: collectResources, type:Exec) {
    commandLine "chmod", "-R", "+rw", "${packageResDirectory}"
}

task createDefoldPackage(dependsOn: fixResPermissions, type: Tar) {
    from packageOutputDirectory
    archiveName 'android-support-v4-armv7-android.tar.gz'
    compression = Compression.GZIP
    destinationDir file("./")
}

build.dependsOn createDefoldPackage

clean {
    delete "${project.buildDir}"
}
